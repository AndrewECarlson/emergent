// This is a test case from sentence gestalt model

Sentence {
	Ate
}

Ate {
	Std:A Ate:V Ate:P Ate:M
}

Ate:V {
	%80 'ate'
	%20 'consumed'
}

// Std agent used for most actions
Std:A {
	Busdriver:A
	Teacher:A
	PitcherPers:A
	Schoolgirl:A
}

Busdriver:A {
	%70 'busdriver'
	%20 'adult'
	%10 'someone'
}

Teacher:A {
	%70 'teacher'
	%20 'adult'
	%10 'someone'
}

PitcherPers:A {
	%70 'pitcherpers'
	%20 'child'
	%10 'someone'
}

Schoolgirl:A {
	%70 'schoolgirl'
	%20 'child'
	%10 'someone'
}

Ate:P ? {
	Busdriver:A {
		%70 Steak:P
		%10 Soup:P
		%10 IceCream:P
		%10 Crackers:P
	}
	Teacher:A {
		%70 Soup:P
		%10 Steak:P
		%10 IceCream:P
		%10 Crackers:P
	}
	PitcherPers:A {
		%25 Soup:P
		%25 Steak:P
		%25 IceCream:P
		%25 Crackers:P
	}
	Schoolgirl:A {
		%25 Soup:P
		%25 Steak:P
		%25 IceCream:P
		%25 Crackers:P
	}
}

Steak:P {
	%70 'steak'
	%20 'food'
	%10 'something'
}

Soup:P {
	%70 'soup'
	%20 'food'
	%10 'something'
}

IceCream:P {
	%70 'icecream'
	%20 'food'
	%10 'something'
}

Crackers:P {
	%70 'crackers'
	%20 'food'
	%10 'something'
}

Ate:M {
	Ate:Pi
	Ate:AoPc
	Ate:L
	Ate:R
}

Ate:Pi ? {
	Steak:P {
		%70 'with' 'knife'
		%20 'with' 'utensil'
		%10 'with' 'something'
	}
	Soup:P || IceCream:P {
		%70 'with' 'spoon'
		%20 'with' 'utensil'
		%10 'with' 'something'
	}
	Crackers:P {
		%80 'with' 'finger'
		%20 'with' 'something'
	}
}

Ate:AoPc ? {
	Steak:P || IceCream:P { Ate:Ao }
	Soup:P  || Crackers:P { Ate:Pc }
}

Ate:Ao ? {
	Busdriver:A { 'with' Teacher:A }
	Teacher:A { 'with' Busdriver:A }
	Schoolgirl:A { 'with' PitcherPers:A }
	PitcherPers:A { 'with' Schoolgirl:A }
}

Ate:Pc ? {
	Soup:P {
		%80 'with' 'crackers'
		%20 'with' 'something'
	}
	Crackers:P {
		%80 'with' 'jelly'
		%20 'with' 'something'
	}
}

Ate:L ? {
	Steak:P || Soup:P || Crackers:P {	'in' 'kitchen' }
	IceCream:P {	'in' 'park' }
}

Ate:R ? {
	(Busdriver:A || PitcherPers:A) && (Steak:P || Soup:P || Crackers:P) { 'with' 'gusto' }
	(Teacher:A || Schoolgirl:A) && (Steak:P || Soup:P || Crackers:P) { 'with' 'daintiness' }
	IceCream:P {	'with' 'pleasure' }
}


